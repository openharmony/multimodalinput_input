# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/multimodalinput/input/multimodalinput_mini.gni")
copy_taihe_idl("infrared_emitter_taihe") {
  sources = [ "idl/ohos.multimodalInput.infraredEmitter.taihe" ]
}
config("infraredEmitter_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "include",
  ]
}
subsystem_name = "multimodalinput"
part_name = "input"
taihe_generated_file_path_infraredEmitter = "$taihe_file_path/out/$subsystem_name/$part_name/infraredEmitter"
ohos_taihe("run_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path_infraredEmitter}"
  deps = [ ":infrared_emitter_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.multimodalInput.infraredEmitter.ani.cpp",
    "$taihe_generated_file_path/src/ohos.multimodalInput.infraredEmitter.abi.c",
  ]
}
taihe_shared_library("InfraredEmitter") {
  taihe_generated_file_path = "${taihe_generated_file_path_infraredEmitter}"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  configs = [
    "${mmi_path}:coverage_flags",
    ":infraredEmitter_config",
  ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.multimodalInput.infraredEmitter.impl.cpp",
  ]
  deps = [
    ":run_taihe",
    "${mmi_path}/frameworks/proxy:libmmi-client",
    "${mmi_path}/util:libmmi-util",
  ]
  branch_protector_ret = "pac_ret"
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
}
generate_static_abc("infrared_emitter") {
  base_url = "${taihe_generated_file_path_infraredEmitter}"
  files = [ "${taihe_generated_file_path_infraredEmitter}/@ohos.multimodalInput.infraredEmitter.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/infrared_emitter.abc"
  dependencies = [ ":run_taihe" ]
}
ohos_prebuilt_etc("infrared_emitter_etc") {
  source = "$target_out_dir/infrared_emitter.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":infrared_emitter" ]
}

generate_static_abc("input_infrared_emitter_taihe_test") {
  base_url = "${mmi_path}/frameworks/ets/infrared_emitter"
  files = [ "$base_url/test/test_main.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/infrared_emitter_test.abc"
}

group("ets_infraredEmitter_package") {
  deps = [
    ":infrared_emitter_etc",
    ":InfraredEmitter",
  ]
}