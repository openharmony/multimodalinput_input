# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

gen_src_dir = "//third_party/mtdev"
gen_dst_dir = root_out_dir + "/diff_mtdev_hdf"
patches_root_dir = "//foundation/multimodalinput/input/patch"
build_gn_dir = "$patches_root_dir/diff_mtdev_hdf"

action("apply_patch") {
  visibility = [ ":*" ]
  script = "$patches_root_dir/apply_patch.sh"
  inputs = [ "$gen_src_dir" ]
  outputs = [
    "$gen_dst_dir/include",
    "$gen_dst_dir/src",
    "$gen_dst_dir/src/caps.c",
    "$gen_dst_dir/src/core.c",
    "$gen_dst_dir/src/iobuf.c",
    "$gen_dst_dir/src/match.c",
    "$gen_dst_dir/src/match_four.c",
  ]
  args = [
    rebase_path(gen_src_dir, root_build_dir),
    rebase_path(gen_dst_dir, root_build_dir),
    rebase_path(build_gn_dir, root_build_dir),
  ]
}

config("libmtdev_config") {
  visibility = [ ":*" ]

  include_dirs = [ "$gen_dst_dir/src" ]

  cflags = [
    "-Wno-unused-parameter",
    "-Wno-sign-compare",
  ]
}

config("libmtdev_public_config") {
  include_dirs = [ "$gen_dst_dir/include" ]

  cflags = []
}

ohos_source_set("patch_gen_mtdev-third") {
  sources = [
    "$gen_dst_dir/src/caps.c",
    "$gen_dst_dir/src/core.c",
    "$gen_dst_dir/src/iobuf.c",
    "$gen_dst_dir/src/match.c",
    "$gen_dst_dir/src/match_four.c",
  ]

  configs = [ ":libmtdev_config" ]

  deps = [ ":apply_patch" ]

  public_deps = []
}

ohos_shared_library("libmtdev-hdf") {
  sources = []

  configs = [ ":libmtdev_config" ]

  deps = []

  public_deps = [ ":patch_gen_mtdev-third" ]

  part_name = "input"
}
