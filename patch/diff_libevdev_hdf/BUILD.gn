# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

gen_src_dir = "//third_party/libevdev"
gen_dst_dir = root_out_dir + "/diff_libevdev_hdf"
patches_root_dir = "//foundation/multimodalinput/input/patch"
build_gn_dir = "$patches_root_dir/diff_libevdev_hdf"

action("apply_patch") {
  visibility = [ ":*" ]
  script = "$patches_root_dir/apply_patch.sh"
  inputs = [ "$gen_src_dir" ]
  outputs = [
    "$gen_dst_dir/export_include",
    "$gen_dst_dir/libevdev",
    "$gen_dst_dir/include",
    "$gen_dst_dir/libevdev/libevdev-uinput.c",
    "$gen_dst_dir/libevdev/libevdev.c",
    "$gen_dst_dir/libevdev/libevdev-names.c",
  ]
  args = [
    rebase_path(gen_src_dir, root_build_dir),
    rebase_path(gen_dst_dir, root_build_dir),
    rebase_path(build_gn_dir, root_build_dir),
  ]
}

config("libevdev_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "$gen_dst_dir/libevdev",
    "$gen_dst_dir/include",
  ]

  cflags = [
    "-Wno-unused-parameter",
    "-Wno-missing-braces",
  ]
}

config("libevdev_public_config") {
  include_dirs = [ "$gen_dst_dir/export_include" ]

  cflags = []
}

ohos_source_set("patch_gen_libevdev") {
  sources = [
    "$gen_dst_dir/libevdev/libevdev-names.c",
    "$gen_dst_dir/libevdev/libevdev-uinput.c",
    "$gen_dst_dir/libevdev/libevdev.c",
  ]

  configs = [ ":libevdev_config" ]

  public_configs = [ ":libevdev_public_config" ]

  deps = [ ":apply_patch" ]

  public_deps = []
}

ohos_shared_library("libevdev-hdf") {
  sources = []

  configs = [ ":libevdev_config" ]

  public_configs = [ ":libevdev_public_config" ]

  deps = [ ":patch_gen_libevdev" ]

  public_deps = []

  part_name = "input"
}
